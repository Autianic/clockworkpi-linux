# U-Boot: ClockworkPI A06 based on PKGBUILD for RockPro64
# Maintainer: Dan Johansen <strit@manjaro.org>
# Contributor: Max Fierke <max@maxfierke.com>
# Contributor: Michael Gollnick
# Contributor: Kevin Mihelich
# Contributor: Adam <adam900710@gmail.com>

pkgname=uboot-clockworkpi-a06
pkgver=2022.04
pkgrel=3
pkgdesc="U-Boot for ClockworkPI A06"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('git' 'dtc' 'bc' 'qemu-user' 'rkbin-aarch64-hack')
provides=('uboot')
conflicts=('uboot')
install=${pkgname}.install
groups=('clockworkpi-a06')
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "https://github.com/rockchip-linux/rkbin/archive/refs/heads/master.zip"
        "0001-uboot-clockworkpi-a06.patch"
        "0002-pgwipeout-mmc-sdhci-sdma-spl.patch"
        "0003-RKTRUST.patch")
# mainline ATF: "https://github.com/rockchip-linux/rkbin/archive/refs/heads/master.zip"
sha256sums=('68e065413926778e276ec3abd28bb32fa82abaa4a6898d570c1f48fbdb08bcd0'
            'SKIP'
            '7e409c95d72dd1f54b88d56c00dd1cf44875f4c9eb0b90a37dfb75b863aff415'
            '9e2287b5fd668d87b6cb9f3580b60f41dbf3aa01b583cda17709899e3ced7e46'
            '2d2cda003764fdb8030814005b26bf74e928ec884440c18ad07edad2ebff52ee')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  patch -Np1 -i "${srcdir}/0001-uboot-clockworkpi-a06.patch"
  patch -Np1 -i "${srcdir}/0002-pgwipeout-mmc-sdhci-sdma-spl.patch"
  cd ../rkbin-master
  patch -Np1 -i "${srcdir}/0003-RKTRUST.patch"
}

build() {
  # Avoid build warnings by editing a .config option in place instead of
  # appending an option to .config, if an option is already present
  update_config() {
    if ! grep -q "^$1=$2$" .config; then
      if grep -q "^# $1 is not set$" .config; then
        sed -i -e "s/^# $1 is not set$/$1=$2/g" .config
      elif grep -q "^$1=" .config; then
        sed -i -e "s/^$1=.*/$1=$2/g" .config
      else
        echo "$1=$2" >> .config
      fi
    fi
  }

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

# RKBIN TFA
  echo -e "\nUsing closed-source TF-A from Rockchip...\n"
  cp -v ${srcdir}/rkbin-master/bin/rk33/rk3399_bl31_v1.36.elf ${srcdir}/u-boot-${pkgver/rc/-rc}/bl31.elf

# mainline TFA
# echo -e "\nBuilding TF-A for ClockworkPI A06...\n"

# DEBUG version
# make PLAT=rk3399 LOG_LEVEL=50 DEBUG=1 V=1
# cp -v build/rk3399/debug/bl31/bl31.elf ${srcdir}/u-boot-${pkgver/rc/-rc}/bl31.elf

# RELEASE version
# make PLAT=rk3399
# cp -v build/rk3399/release/bl31/bl31.elf ${srcdir}/u-boot-${pkgver/rc/-rc}/bl31.elf

  cd ${srcdir}/u-boot-${pkgver/rc/-rc}

  echo -e "\nBuilding U-Boot for ClockworkPI A06...\n"
  make clockworkpi-a06-rk3399_defconfig

  update_config 'CONFIG_IDENT_STRING' '" Arch Linux ARM-BSP"'
  update_config 'CONFIG_OF_LIBFDT_OVERLAY' 'y'
  update_config 'CONFIG_SPL_MMC_SDHCI_SDMA' 'y'
  make EXTRAVERSION=-$(date -R | cut -c18-25) u-boot-dtb.bin

  cd "${srcdir}/rkbin-master"
  qemu-x86_64 tools/mkimage -n rk3399 -T rksd -d bin/rk33/rk3399_ddr_666MHz_v1.30.bin idbloader-ddr-666MHz.img
  cat bin/rk33/rk3399_miniloader_v1.30.bin >> idbloader-ddr-666MHz.img
  qemu-x86_64 tools/mkimage -n rk3399 -T rksd -d bin/rk33/rk3399_ddr_800MHz_v1.30.bin idbloader-ddr-800MHz.img
  cat bin/rk33/rk3399_miniloader_v1.30.bin >> idbloader-ddr-800MHz.img
  qemu-x86_64 tools/mkimage -n rk3399 -T rksd -d bin/rk33/rk3399_ddr_933MHz_v1.30.bin idbloader-ddr-933MHz.img
  cat bin/rk33/rk3399_miniloader_v1.30.bin >> idbloader-ddr-933MHz.img

  qemu-x86_64 tools/loaderimage --pack --uboot ../u-boot-2022.04/u-boot-dtb.bin uboot.img 0x00200000
  qemu-x86_64 tools/trust_merger RKTRUST/RK3399TRUST.ini
}

package() {
  cd rkbin-master

  mkdir -p "${pkgdir}/boot/extlinux"

  install -D -m 0644 idbloader-ddr-666MHz.img idbloader-ddr-800MHz.img idbloader-ddr-933MHz.img uboot.img trust.img -t "${pkgdir}/boot"
}
