# U-Boot: ClockworkPI A06 based on PKGBUILD for RockPro64
# Maintainer: Dan Johansen <strit@manjaro.org>
# Contributor: Max Fierke <max@maxfierke.com>
# Contributor: Michael Gollnick
# Contributor: Kevin Mihelich
# Contributor: Adam <adam900710@gmail.com>

pkgname=uboot-clockworkpi-a06
pkgver=2022.04
pkgrel=3
_tfaver=2.7
pkgdesc="U-Boot for ClockworkPI A06"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('git' 'dtc' 'bc')
provides=('uboot')
conflicts=('uboot')
install=${pkgname}.install
groups=('clockworkpi-a06')
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "https://git.trustedfirmware.org/TF-A/trusted-firmware-a.git/snapshot/trusted-firmware-a-$_tfaver.tar.gz"
        "0001-uboot-clockworkpi-a06.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  patch -Np1 -i "${srcdir}/0001-uboot-clockworkpi-a06.patch"
}

build() {
  # Avoid build warnings by editing a .config option in place instead of
  # appending an option to .config, if an option is already present
  update_config() {
    if ! grep -q "^$1=$2$" .config; then
      if grep -q "^# $1 is not set$" .config; then
        sed -i -e "s/^# $1 is not set$/$1=$2/g" .config
      elif grep -q "^$1=" .config; then
        sed -i -e "s/^$1=.*/$1=$2/g" .config
      else
        echo "$1=$2" >> .config
      fi
    fi
  }

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd ${srcdir}/trusted-firmware-a-$_tfaver

  echo -e "\nBuilding TF-A for ClockworkPI A06...\n"

# DEBUG version
#  make PLAT=rk3399 LOG_LEVEL=50 DEBUG=1 V=1
#  cp -v build/rk3399/debug/bl31/bl31.elf ${srcdir}/u-boot-${pkgver/rc/-rc}/bl31.elf

# RELEASE version
  make PLAT=rk3399
  cp -v build/rk3399/release/bl31/bl31.elf ${srcdir}/u-boot-${pkgver/rc/-rc}/bl31.elf

  cd ${srcdir}/u-boot-${pkgver/rc/-rc}

  echo -e "\nBuilding U-Boot for ClockworkPI A06...\n"
  make clockworkpi-a06-rk3399_defconfig

  update_config 'CONFIG_IDENT_STRING' '" Arch Linux ARM"'
  update_config 'CONFIG_OF_LIBFDT_OVERLAY' 'y'
# make EXTRAVERSION=-${pkgrel}
  make EXTRAVERSION=-$(date -R | cut -c18-25)
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}/boot/extlinux"

  install -D -m 0644 idbloader.img u-boot.itb -t "${pkgdir}/boot"
}
